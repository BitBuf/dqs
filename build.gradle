plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'dev.dewy'
version '3.1.0'

repositories {
    mavenCentral()
    jcenter()

    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
}

dependencies {
    implementation group: 'io.netty', name: 'netty-all', version: '4.1.25.Final'

    implementation group: 'com.google.guava', name: 'guava', version: '20.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'

    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.4.0'

    implementation group: 'net.daporkchop.lib', name: 'hash', version: '0.5.5-SNAPSHOT'
    implementation group: 'net.daporkchop.lib', name: 'logging', version: '0.5.5-SNAPSHOT'
    implementation group: 'net.daporkchop.lib', name: 'minecraft-text', version: '0.5.5-SNAPSHOT'

    implementation ('net.dv8tion:JDA:4.2.0_180') {
        exclude module: 'opus-java'
    }

    implementation group: 'com.jagrosh', name: 'jda-utilities', version: '3.0.4'
}

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes("Implementation-Title": "DQS",
                "Implementation-Version": "3.1.0",
                "Main-Class": "dev.dewy.dqs.DQS")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

shadowJar {
    classifier = 'dist'
}

build.dependsOn(shadowJar, sourcesJar)